## Análisis numero de tesis por autor
[source, python]
----
MATCH (n:Person) where (n)-[:AUTHOR]->() 
WITH  n, size((n)-[:AUTHOR]->()) as thCount
RETURN max(thCount), min(thCount), avg(thCount), percentileCont(thCount,0.96)
----

## Autores de multiples tesis
[source, python]
----
MATCH (n:Person) where size((n)-[:AUTHOR]->()) > 1
RETURN count(n)
----

## Autor de 5 tesis (detalle)

[source, python]
----
match (p:Person { name : "Saban Cuño, Mario Javier"})-[a:AUTHOR]->(t)-[id:IN_DEPT]->(d)-[bt:BELONGS_TO]->(uni)
return *
----


## Conectividad de presidentes (red social)

[source, python]
----
MATCH (p:Person) where (p)-[:JURY_MEMBER { role: "President"}]->() 
MATCH (p)-[:SUPERVISOR|JURY_MEMBER|AUTHOR*..2]-(con:Person) 
WITH p, count(distinct con) as con
RETURN max(con), min(con), avg(con), percentileCont(con,0.75), percentileCont(con,0.9)
----

## Camino mas corto entre X e Y 

[source, python]
----
match p = shortestPath((:Person { name : "Gomez Perez, Asuncion"})-[:AUTHOR|JURY_MEMBER|SUPERVISOR*]-(:Person { name : "Sanchez Jimenez, Rodrigo"})) 
return p
----


## your "Sánchez number"

[source, python]
----
MATCH p = shortestPath((:Person { name : "Sanchez Perez-castejon, Pedro"})-[:AUTHOR|JURY_MEMBER|SUPERVISOR*]-(:Person { name : "Sanchez Jimenez, Rodrigo"})) return p
----

## time between my dissertation and the dissertation of my first supervised thesis

[source, python]
----
MATCH (sup:PhDThesis)<-[:SUPERVISOR]-(n:Person)-[:AUTHOR]->(my:PhDThesis)
where sup.dissertationDate is not null  and my.dissertationDate is not null
and sup.dissertationDate=~ "\d{4}\-\d{2}\-\d{2}" and my.dissertationDate=~ "\d{4}\-\d{2}\-\d{2}"
with  duration.between(date(my.dissertationDate),date(sup.dissertationDate)) as delta 
RETURN max(delta), min(delta), avg(delta)
----

## gender analysis

### male/female ratio in authors and supervisors

[source, python]
----
match (p:Person)-[:SUPERVISOR]->()
return p.gender, count(*)
----

[source, python]
----
match (p:Person)-[:AUTHOR]->()
return p.gender, count(*)
----

### supervised theses by gender
[source, python]
----
match (p:Person {gender:"Female"}) with size((p)-[:SUPERVISOR]->()) as supCount
return "F" as gender, min(supCount), max(supCount), avg(supCount)
union
match (p:Person {gender:"Male"}) with size((p)-[:SUPERVISOR]->()) as supCount
return "M" as gender, min(supCount), max(supCount), avg(supCount)
----


### gender affinity author/supervisor by year
[source, python]
----
unwind range(2015,2021) as year
match (au:Person)-[:AUTHOR]->(p:PhDThesis { year: year })<-[:SUPERVISOR]-(sup:Person) 
with year, au.gender as author_gender, sup.gender as supervisor_gender, count(*) as count
where author_gender in ["Male","Female"] and supervisor_gender in ["Male","Female"]
with  year, collect({key : "A:"+author_gender +"/S:"+supervisor_gender , val:count}) as dt
with  year, dt, apoc.coll.sum([x in dt | x.val ]) as yeartotal
return year, [x in dt where x.key = "A:Female/S:Female" | x.val ][0] / yeartotal  as ` A:F-S:F`,
[x in dt where x.key = "A:Female/S:Male" | x.val ][0] / yeartotal  as ` A:F-S:M`,
[x in dt where x.key = "A:Male/S:Female" | x.val ][0] / yeartotal  as ` A:M-S:F`,
[x in dt where x.key = "A:Male/S:Male" | x.val ][0] / yeartotal  as ` A:M-S:M`
----
